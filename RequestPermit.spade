Process 'RequestPermit'

// HIGH LEVEL REQUIREMENTS 

The following applies: 
    "civilian has requested a building permit"
    and 
    "height has been evaluated in order to approve request or not"
    and
    "reason on approval decision has been provided to the civilian"
    and  
    "civilian has received an invoice"
    and 
    "pdf format document has been generated" 
    and 
    "civilian has received an email with pdf attachment"

// DETAILS OF THE REQUIREMENTS

"civilian has requested a building permit" =
    One PERMIT exists in PERMITS with:
        start_date   = input from CIVILIAN
        request_date = currentDate()
        height       = input from CIVILIAN 
        blueprint    = input from CIVILIAN
         
"height has been evaluated in order to approve request or not" =
    if "the height is between 3 and 5 both inclusive"
    then "permit can be automatically approved"
    else "permit needs to be manually approved"

"the height is between 3 and 5 both inclusive" = 
    PERMIT.height >= 3 and PERMIT.height <=5

"permit can be automatically approved" = 
    PERMIT.approved = 'AUTO YES'

"permit needs to be manually approved" = 
    PERMIT.approved = input from APPROVER 
                      of type text 
                      labeled 'Choice: Approve Permit?'
                      chosen from ['YES', 'NO']
                      based on #(CIVILIAN, PERMIT.request_date, PERMIT.start_date, PERMIT.height, PERMIT.blueprint)
                      default 'NO'

"reason on approval decision has been provided to the civilian" =  
    if PERMIT.approved == 'NO'
    then "explanation has to be provided"
    else "congratulation message"

"explanation has to be provided" =
    PERMIT.explanation = input from APPROVER 
                         of type text 
                         labeled 'Provide explanation on your negative response:'
                         based on #(CIVILIAN, PERMIT.request_date, PERMIT.start_date, PERMIT.height, PERMIT.blueprint, PERMIT.approved)

"congratulation message" = 
    PERMIT.explanation = 'We would like to congratulate you on your permit approval.' 

"pdf format document has been generated" =  
    One PDF_FILE exists in DOCUMENTS with:
        name = 'C:/Users/Brigel/Desktop/Repositories/SPADE---Building-Permit/temp/output/Building_Permit_with_start_date_{PERMIT.start_date}.pdf'
        template = 'C:/Users/Brigel/Desktop/Repositories/SPADE---Building-Permit/temp/templates/PermitResponse.docx'
        type = 'pdf'
        parameters = #(
                       'last_name'   : CIVILIAN.lastName, 
                       'first_name'  : CIVILIAN.firstName,
                       'start_date'  : PERMIT.start_date,
                       'request_date': PERMIT.request_date,
                       'height'      : PERMIT.height, 
                       'explanation' : PERMIT.explanation, 
                       'decision'    : if PERMIT.approved == 'NO' then 'NO' else 'YES'
                       ) 